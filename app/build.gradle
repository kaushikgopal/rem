apply plugin: 'com.android.application'
apply from: '../config/quality.gradle'

def gitSha() {
    def res = 'git rev-parse HEAD'.execute([], project.rootDir).text.trim()

    def diff = 'git diff'.execute([], project.rootDir).text.trim()
    if (diff != null && diff.length() > 0) {
        res += "-dirty"
    }

    return res
}

def buildTime = new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.0"

    defaultConfig {
        applicationId "co.kaush.rem"
        minSdkVersion 15
        targetSdkVersion 22 // Not tested on API 23 yet
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'LICENSE'
        exclude 'NOTICE'
        exclude 'asm-license.txt'
        exclude 'LICENSE.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    // For Butterknife
    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {

    // -----------------------------------------
    // Support dependencies

    compile 'org.jetbrains:annotations:13.0'
    compile 'com.android.support:appcompat-v7:23.0.0'
    compile('com.android.support:recyclerview-v7:23.0.0') {
        exclude module: 'support-v4'
        exclude module: 'support-annotations'
    }

    // -----------------------------------------
    // 3rd party libs

    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.jakewharton.timber:timber:2.4.2'
    compile 'com.darwinsys:hirondelle-date4j:1.5.1'
    compile 'com.squareup.okhttp:okhttp:2.4.0'
    compile('com.squareup.okhttp:okhttp-urlconnection:2.4.0') {
        exclude group: 'com.squareup.okhttp', module: 'okhttp'
    }
    compile 'io.reactivex:rxandroid:1.0.1'
    compile 'com.squareup.dagger:dagger:1.2.2'
    provided 'com.squareup.dagger:dagger-compiler:1.2.2'
//    compile 'com.squareup.retrofit:retrofit:1.9.0'
//    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.sqlbrite:sqlbrite:0.2.1'
    compile 'uk.co.chrisjenx:calligraphy:2.1.0'
    compile("com.doomonafireball.betterpickers:library:1.6.0") {
        exclude group: 'com.android.support', module: 'support-v4'
    }

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3'

    // -----------------------------------------
    // TESTING

    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-core:1.9.5"
    testCompile 'com.google.truth:truth:0.27'

    // Set this dependency to use JUnit 4 rules
    androidTestCompile('com.android.support.test:rules:0.2') {
        exclude module: 'support-annotations'
    }

    // Set this dependency to build and run Espresso tests
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.1') {
        exclude module: 'support-annotations'
    }

    // Set this dependency to build and run UI Automator tests
    // androidTestCompile 'com.android.support.test.uiautomator:uiautomator-v18:2.1.0'

    androidTestCompile('com.squareup.assertj:assertj-android:1.0.0') {
        exclude module: 'support-annotations'
    }
    androidTestCompile 'org.mockito:mockito-core:1.9.5'
}